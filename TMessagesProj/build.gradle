import org.eclipse.jgit.internal.storage.file.FileRepository

plugins {
    id 'kotlin-android'
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

configurations {
    compile.exclude module: 'support-v4'
}

configurations.all {
    exclude group: 'com.google.firebase', module: 'firebase-core'
    exclude group: 'androidx.recyclerview', module: 'recyclerview'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.sharetarget:sharetarget:1.2.0-rc01"
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'com.google.android.gms:play-services-measurement:21.0.0'

    compileOnly 'org.checkerframework:checker-qual:3.22.0'
    compileOnly 'org.checkerframework:checker-compat-qual:2.5.5'
    implementation 'com.google.firebase:firebase-messaging:23.0.4'
    implementation 'com.google.firebase:firebase-config:21.1.0'
    implementation 'com.google.firebase:firebase-datatransport:18.1.3'
    implementation 'com.google.firebase:firebase-appindexing:20.0.0'
    implementation 'com.google.android.gms:play-services-maps:18.0.2'
    implementation 'com.google.android.gms:play-services-auth:20.2.0'
    implementation 'com.google.android.gms:play-services-auth-api-phone:18.0.1'
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.android.gms:play-services-wearable:17.1.0'
    implementation 'com.google.android.gms:play-services-location:19.0.1'
    implementation 'com.google.android.gms:play-services-wallet:19.1.0'
    implementation 'com.googlecode.mp4parser:isoparser:1.0.6'
    implementation 'com.stripe:stripe-android:2.0.2'
    implementation 'com.google.mlkit:language-id:17.0.3'
    implementation files('libs/libgsaverification-client.aar')

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.21"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1"
}

ext.abiCodes = ['arm64-v8a': 1, 'armeabi-v7a': 2, 'x86_64': 3, 'x86': 4, 'universal': 9]

def commitId = new FileRepository(rootProject.file(".git")).getRefDatabase().exactRef("HEAD").getObjectId().name()

android {
    compileSdkVersion 32
    buildToolsVersion '32.1.0-rc1'
    ndkVersion "21.4.7075529"
    namespace 'org.telegram.messenger'

    sourceSets.main.jniLibs.srcDirs = ['./jni/']

    externalNativeBuild {
        cmake {
            path 'jni/CMakeLists.txt'
        }
    }

    lint {
        checkReleaseBuilds false
        disable 'MissingTranslation', 'ExtraTranslation', 'BlockedPrivateApi'
    }

    packagingOptions {
        resources {
            excludes += ['**', 'META-INF/DEPENDENCIES']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11

        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = 11
    }

    Properties signingProperties = new Properties()
    if (project.rootProject.file('signing.properties').exists()) {
        signingProperties.load(project.rootProject.file('signing.properties').newDataInputStream())
    }

    signingConfigs {
        if (signingProperties.getProperty("storeFile") != null) {
            sign {
                storeFile file(signingProperties.getProperty("storeFile"))
                storePassword signingProperties.getProperty("storePassword")
                keyAlias signingProperties.getProperty("keyAlias")
                keyPassword signingProperties.getProperty("keyPassword")
            }
        }
    }

    buildTypes {
        release {
            if (signingProperties.getProperty("storeFile") != null) {
                signingConfig signingConfigs.sign
            }
            minifyEnabled true
            shrinkResources true
            multiDexEnabled true
            zipAlignEnabled true
            proguardFiles 'proguard-rules.pro'
            buildConfigField 'String', 'COMMIT_ID', "\"${commitId}\""
            buildConfigField 'String', 'BUILD_DATE', "\"" + System.currentTimeMillis().toString() + "\""

            resValue "string", "AppNameX", "@string/MoeAppName"
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher"
            ]
        }
        debug {
            debuggable true
            jniDebuggable true
            if (signingProperties.getProperty("storeFile") != null) {
                signingConfig signingConfigs.sign
            }
            minifyEnabled true
            shrinkResources true
            multiDexEnabled true
            zipAlignEnabled true
            proguardFiles 'proguard-rules.pro'
            buildConfigField 'boolean', 'DEBUG_PRIVATE_VERSION', "true"
            buildConfigField 'String', 'COMMIT_ID', "\"${commitId}\""
            buildConfigField 'String', 'BUILD_DATE', "\"" + System.currentTimeMillis().toString() + "\""

            resValue "string", "AppNameX", "@string/MoeAppNameBeta"
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_beta"
            ]
        }
    }
    
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def abi = output.getFilter('ABI')
            if (abi == null) abi = 'universal'

            def date = new Date()
            def formattedDate = date.format('MMddyyyy')

            outputFileName = "Moegram-${formattedDate}-${abi}.apk"
            def abiVersionCode = project.ext.abiCodes.get(abi)
            output.versionCodeOverride = defaultConfig.versionCode * 10 + abiVersionCode
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32
        versionName "8.7.4.2"
        versionCode = 2640

        applicationId "com.moegram.messenger"

        externalNativeBuild {
            cmake {
                version '3.22.1'
                arguments '-DANDROID_STL=c++_static', '-DANDROID_PLATFORM=android-21'
            }
        }
    }
    bundle {
        language {
            enableSplit = false
        }
    }

    splits.abi {
        enable true
        universalApk true
    }

    dependenciesInfo.includeInApk false
}
